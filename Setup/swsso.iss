; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "swSSO"
#define MyAppVersion "1.24"
#define MyAppURL "www.swsso.fr"
#define MyAppExeName "swSSO.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8266EBBD-3ECD-4FFA-9B6B-9E44CE242E70}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} v{#MyAppVersion}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=licence.txt
Compression=lzma
SolidCompression=yes

[Languages]
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "startupicon"; Description: "{cm:CreateStartupIcon}"

[Files]
Source: "c:\dev\swSSO\Release\swSSO.exe"; DestDir: "{app}"; Flags: ignoreversion uninsrestartdelete
Source: "c:\dev\swSSO\Release\swSSOCM.dll"; DestDir: "{app}"; Flags: ignoreversion uninsrestartdelete; Check: not IsWin64; AfterInstall: RegisterCM('{app}\swSSOCM.dll')
Source: "c:\dev\swSSO\Release\swSSOSVC.exe"; DestDir: "{app}"; Flags: ignoreversion uninsrestartdelete
Source: "c:\dev\swSSO\x64\Release\swSSOCM.dll"; DestDir: "{app}"; Flags: ignoreversion uninsrestartdelete; Check: IsWin64; AfterInstall: RegisterCM('{app}\swSSOCM.dll')
Source: "c:\dev\swSSO\Release\swSSOHotKey.dll"; DestDir: "{app}"; Flags: ignoreversion uninsrestartdelete

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Parameters: "%appdata%\swSSO\swSSO.ini"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Parameters: "%appdata%\swSSO\swSSO.ini"; Tasks: desktopicon
Name: "{commonstartup}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Parameters: "%appdata%\swSSO\swSSO.ini"; Tasks: startupicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Parameters: "%appdata%\swSSO\swSSO.ini"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\swSSOSVC.exe"; Parameters: "install"

[UninstallRun]
Filename: "{app}\swSSOSVC.exe"; Parameters: "uninstall"

[Registry]
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\services\swSSOCM"; ValueType: none; Flags: uninsdeletekey
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\services\swSSOCM\NetworkProvider"; ValueType: none; Flags: uninsdeletekey
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\services\swSSOCM\NetworkProvider"; ValueType: string; ValueName: "AuthentProviderPath"; ValueData: "{app}\swssoCM.dll"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\services\swSSOCM\NetworkProvider"; ValueType: dword; ValueName: "Class"; ValueData: "2"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\services\swSSOCM\NetworkProvider"; ValueType: string; ValueName: "Name"; ValueData: "swSSOCM"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\services\swSSOCM\NetworkProvider"; ValueType: string; ValueName: "ProviderPath"; ValueData: "{app}\swssoCM.dll"; Flags: uninsdeletevalue
Root: "HKLM32"; Subkey: "SOFTWARE\swSSO\GlobalPolicy"; Flags: uninsdeletekey
Root: "HKLM32"; Subkey: "SOFTWARE\swSSO\GlobalPolicy"; ValueType: dword; ValueName: "PasswordChoiceLevel"; ValueData: "4"; Flags: uninsdeletevalue
Root: "HKLM32"; Subkey: "SOFTWARE\swSSO\SVC"; ValueType: none; Flags: uninsdeletekey
Root: "HKLM32"; Subkey: "SOFTWARE\swSSO\SVC"; ValueType: string; ValueName: "swSSOClient"; ValueData: "C987BBC0AC4F93CF6E72FDED23726EDC69883A7EB61DDFC485702DB7EEEBC24B"; Flags: uninsdeletevalue

[CustomMessages]
french.CreateStartupIcon=Lancer swSSO au démarrage de Windows
english.CreateStartupIcon=Run swSSO at startup

[Code]
procedure RegisterCM(FileName: String);
var
  strProviderOrder: String;
begin
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\NetworkProvider\Order',
     'ProviderOrder', strProviderOrder) then
  begin
    //MsgBox('strProviderOrder=' + strProviderOrder, mbInformation, MB_OK);
    // si swSSOCM déjà présent on ne fait rien, sinon on l'ajoute
    if Pos('swSSOCM',strProviderOrder)=0 then
    begin
      RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\NetworkProvider\Order',
       'ProviderOrder', strProviderOrder+',swSSOCM');
    end;
  end;
end;

function NeedRestart(): Boolean;
begin
  Result := True;
end;

function UninstallNeedRestart(): Boolean;
begin
  Result := True;
end;
